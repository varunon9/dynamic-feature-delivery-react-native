plugins {
    id 'com.android.dynamic-feature'
}

project.ext.react = [
    entryFile: "index.js",
    root: "$rootDir/INGO-RN-AltAccoJS/",
    bundleInDebug: false,
    hermesCommand: "$rootDir/INGO-RN-AltAccoJS/node_modules/hermes-engine/%OS-BIN%/hermes",
    enableHermes: true // clean and rebuild if changing
]

apply from: "$rootDir/INGO-RN-AltAccoJS/node_modules/react-native/react.gradle"

/**
 * The preferred build flavor of JavaScriptCore.
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = 'org.webkit:android-jsc-intl:+'`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US.  Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = "org.webkit:android-jsc:+"

/**
 * Whether to enable the Hermes VM.
 * This should be set on project.ext.react and mirrored here.  If it is not set
 * on project.ext.react, JavaScript will not be compiled to Hermes Bytecode
 * and the benefits of using Hermes will therefore be sharply reduced.
 */
def enableHermes = project.ext.react.get("enableHermes", false);

android {
    compileSdkVersion 30

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.ingoibibo.ingornaltacco"
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(":app")
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'

    // react-native: https://reactnative.dev/docs/0.61/integration-with-existing-apps
    implementation "com.facebook.react:react-native:+" // From node_modules
    if (enableHermes) {
        def hermesPath = "$rootDir/INGO-RN-AltAccoJS/node_modules/hermes-engine/android/";
        debugImplementation files(hermesPath + "hermes-debug.aar")
        releaseImplementation files(hermesPath + "hermes-release.aar")
    } else {
        implementation jscFlavor
    }
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
}